// Copyright Epic Games, Inc. All Rights Reserved.

#include "Common.ush"
#include "ScreenPass.ush"

Texture2D SceneColor;
SamplerState SceneColorSampler;

Texture2D SeparateTranslucency;
SamplerState SeparateTranslucencySampler;

Texture2D SeparateModulation;
SamplerState SeparateModulationSampler;

SCREEN_PASS_TEXTURE_VIEWPORT(Color)
SCREEN_PASS_TEXTURE_VIEWPORT(Translucency)
FScreenTransform ColorToTranslucency;

float2 ScreenPosToSceneColorUV;
float2 ScreenPosToSeparateTranslucencyUV;
float2 SeparateTranslucencyUVMax;
float2 SeparateTranslucencyExtentInverse;

#if PERMUTATION_NEARESTDEPTHNEIGHBOR 

#include "SeparateTranslucency.ush"

#endif // PERMUTATION_NEARESTDEPTHNEIGHBOR

void MainPS(
	float4 SvPosition : SV_POSITION,
	out float4 OutColor : SV_Target0)
{
	float2 ColorUV = SvPosition.xy * Color_ExtentInverse.xy;
	float2 SeparateTranslucencyUV = clamp(ApplyScreenTransform(ColorUV, ColorToTranslucency), Translucency_UVViewportBilinearMin, Translucency_UVViewportBilinearMax);

	float4 SceneColorSample = SceneColor.SampleLevel(SceneColorSampler, ColorUV, 0);

#if PERMUTATION_NEARESTDEPTHNEIGHBOR 

	NearestDepthNeighborUpsamplingResult UpsampleResult = NearestDepthNeighborUpsampling(SvPosition.xy, ColorUV, Translucency_ExtentInverse);

	float4 SeparateTranslucencySample = 0;
	float4 SeparateModulationSample = 0;
	if (UpsampleResult.bUsePointSampler)
	{
		SeparateTranslucencySample	= SeparateTranslucency.SampleLevel(	GlobalPointClampedSampler, UpsampleResult.UV, 0);
		SeparateModulationSample	= SeparateModulation.SampleLevel(	GlobalPointClampedSampler, UpsampleResult.UV, 0);
	}
	else
	{
		SeparateTranslucencySample	= SeparateTranslucency.SampleLevel(	GlobalBilinearClampedSampler, UpsampleResult.UV, 0);
		SeparateModulationSample	= SeparateModulation.SampleLevel(	GlobalBilinearClampedSampler, UpsampleResult.UV, 0);
	}

#else // PERMUTATION_NEARESTDEPTHNEIGHBOR
	
	float4 SeparateTranslucencySample = SeparateTranslucency.SampleLevel(SeparateTranslucencySampler, SeparateTranslucencyUV, 0);
	float4 SeparateModulationSample   = SeparateModulation.SampleLevel(	 SeparateModulationSampler, SeparateTranslucencyUV, 0);

#endif // PERMUTATION_NEARESTDEPTHNEIGHBOR

	// Final composition
	OutColor.rgb = SceneColorSample.rgb * SeparateTranslucencySample.a * SeparateModulationSample.rgb + SeparateTranslucencySample.rgb;
	OutColor.a   = SceneColorSample.a   * SeparateTranslucencySample.a;

#if !POST_PROCESS_ALPHA
	OutColor.a = 0;
#endif
}
